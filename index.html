<!DOCTYPE html>
<html>
<head>
<title>Bash to ShellJS Translator</title>
<style>
@font-face {
  font-family: Junction;
  src: url("fonts/junction-regular/Junction.otf") format("opentype");
}
blockquote {
  font-style: italic;
  padding: 10px;
}
#hint-box {
  position: absolute;
  z-index: 1;
  right: 10px;
  padding: 5px;
  float: right;
  width: 10%;
}
p.hint-text {
  text-align: left;
  font-style: italic;
  font-size: 10pt;
}
table {
  margin-left: auto;
  margin-right: auto;
}
body, h1, p {
  text-align: center;
  font-family: Junction;
}
h2 {
  font-style: italic;
}
textarea {
  overflow-x: scroll;
  overflow-y: scroll;
  float: left;
  display: inline-block;
  background: white;
}
#checkbox1 {
  text-align: center;
}
.code-box {
  height: auto;
  display: table;
  width: auto;
  display: inline-block;
  margin: 10px;
}
.container {
  height: 100%;
  float: right;
  margin-top: 20px;
  margin-left: 10px;
}
button.selector {
  position: relative;
}
</style>
<script src="lib/ohm/dist/ohm.min.js"></script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js"></script>
<script type="text/ohm-js">
Bash {
  Script
      = (Shebang)? allwhitespace* CmdSequence? semicolon*
  CmdSequence = NonemptyListOf<Cmd, semicolon>
  Shebang = "#!" (~"sh" any)* "sh\n"

  Cmd = NoSemicolonCmd
      | SemicolonCmd
      | ("" ~keyword)

  NoSemicolonCmd
      = IfCommand
      | comment
      | ForCommand
      | WhileCommand
      | CmdWithComment

  SemicolonCmd
      = PipeCmd
      | Export
      | Assignment
      | SimpleCmd

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = ifwithspace Conditional semicolon thenwithspace CmdSequence
  ElseIfThen
      = semicolon elifwithspace Conditional semicolon thenwithspace CmdSequence
  ElseCase
      = semicolon elsewithspace CmdSequence
  EndIf = semicolon "fi"

  Conditional
      = TestCmd   -- test
      | SimpleCmd -- cmd

  ForCommand
      = "for" "((" ControlStruct "))" semicolon dowithspace CmdSequence Done
          -- c_style
      | "for" id "in" (Call | arrayReference) semicolon dowithspace CmdSequence Done          -- for_each

  ControlStruct
      = Assignment ";" id BinaryOp Bashword ";" (~")" Bashword)

  WhileCommand
      = whilewithspace Conditional semicolon dowithspace CmdSequence Done

  whilewithspace = "while" space+
  dowithspace = "do" allwhitespace+

  Done = semicolon "done"

  BinaryOp (Binary operator)
      = Equal | NotEqual | LessThan | GreaterThan | LessThanEq | GreaterThanEq
  Equal         = "=="  | "=" | "-eq"
  NotEqual      = "!="  | "-ne"
  LessThan      = "-lt" | "<"
  GreaterThan   = "-gt" | ">"
  LessThanEq    = "-le" | "<="
  GreaterThanEq = "-ge" | ">="

  UnaryOp (Unary operator)
      = "-z" | "-n" | "-b" | "-c" | "-d" | "-e" | "-f" | "-L" | "-p" | "-S"

  ifwithspace   = "if"   allwhitespace+
  elifwithspace = "elif" allwhitespace+
  elsewithspace = "else" allwhitespace+
  thenwithspace = "then" allwhitespace+

  SequenceCmd
      = NoSemicolonCmd semicolon Cmd -- nosemicolon
      | Cmd semicolon Cmd -- std
      | NoSemicolonCmd semicolon -- noscNull
      | Cmd semicolon -- null

  PipeCmd = (PipeCmd | SimpleCmd) "|" allwhitespace* (PipeCmd | SimpleCmd)

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = SimpleCmdBase Redirect*

  Redirect
      = ">" Bashword
      | ">>" Bashword

  SimpleCmdBase
      = BasicCmd
      | CdCmd
      | PwdCmd
      | LsCmd
      | FindCmd
      | CatCmd
      | WhichCmd
      | TestCmd
      | EchoCmd
      | HeadCmd
      | PushdCmd
      | PopdCmd
      | DirsCmd
      | LnCmd
      | ExitCmd
      | ChmodCmd
      | TouchCmd
      | SortCmd
      | SetCmd
      | SedCmd
      | ExecCmd

  BasicCmd = BasicName options? Bashword+
  CdCmd    = "cd" Bashword?
  PwdCmd   = "pwd"

  TestCmd
      = "test " "!"? UnaryOp Bashword               -- unary
      | "test " "!"? Bashword BinaryOp Bashword     -- binary
      | TestCmd_unaryBracket
      | TestCmd_binaryBracket
  TestCmd_unaryBracket
      = "[[" "!"? UnaryOp Bashword "]]"
      | "[" "!"? UnaryOp Bashword "]"
  TestCmd_binaryBracket
      = "[[" "!"? Bashword BinaryOp Bashword "]]"
      | "[" "!"? Bashword BinaryOp Bashword "]"

  FindCmd  = "find "  Bashword+
  LsCmd    = "ls"     options? Bashword*
  CatCmd   = "cat"    Bashword*
  WhichCmd = "which " Bashword
  EchoCmd  = "echo"   Bashword*
  HeadCmd  = "head"   options? Bashword*
  PushdCmd = "pushd"  options? Bashword?
  PopdCmd  = "popd"   options? Bashword?
  DirsCmd  = "dirs"   (options | Bashword)?
  LnCmd    = "ln "    options? Bashword Bashword
  ExitCmd  = "exit"   Bashword?
  ChmodCmd = "chmod " Bashword Bashword
  TouchCmd = "touch " options? Bashword
  SortCmd  = "sort"   options? Bashword*
  SetCmd   = "set "   modOptions
  SedCmd   = "sed "   sedRegex Bashword*
  ExecCmd  = CmdName Arglist

  sedRegex
      = "'s/" (~"/" ~"'" any)* "/" (~"/" ~"'" any)* ("/" "g"?)? "'"
      | "\"s/" (~"/" ~"\"" any)* "/" (~"/" ~"\"" any)* ("/" "g"?)? "\""
  BasicName
      = "cp "
      | "rm "
      | "mkdir "
      | "mv "
      | "grep "

  CmdName = (~keyword Bashword)
  Arglist = Bashword*
  comment = space* "#" (~"\n" any)*
  options = "-" letter+
  modOptions = ("-" |"+" ) letter+

  Bashword
      = Call
      | ArrayLiteral
      | arrayLength
      | arrayReference
      | stringLiteral
      | number
      | bareWord

  ArrayLiteral
      = "(" allwhitespace* (Bashword allwhitespace*)* ")"

  Call
      = "$(" Cmd ")"
      | "\"$(" Cmd ")\""
      | "`" Cmd "`"
      | "\"`" Cmd "`\""

  arrayReference
      = "${" id "[@]}"
      | "\"${" id "[@]}\""

  arrayLength
      = "${#" id "[@]}"

  reference
      = "$" id -- simple
      | "${" id "}" -- wrapped
      | "${" id ":" digit+ ":"? digit* "}" -- substr
      | "${" id ("//" | "/") alnum* "/"? alnum* "}" -- substit

  number
      = "-"? digit+

  bareWord
      = (~badchars (reference | any))+

  keyword
      = keywordRoot ~alnum

  keywordRoot
      = "if" | "then" | "else" | "elif" | "fi" | "for" | "done" | "do"

  stringLiteral
      = singleString
      | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
      = (~("'" | "\\") any)
      | "\\" any -- escape

  notDoubleQuote
      = (~("\"" | "\\") (reference | any))
      | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\"" | "`" | "(" | ")"
  Export
      = "export " Assignment -- assign
      | "export " id -- bare
  Assignment
      = ("local " | "readonly ")? idEqual Bashword?
  idEqual = id_std "="
  id  = (~keyword (letter | "_") (alnum | "_")*) -- std
      | digit+ -- arg
      | "?" -- error
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon
      = ";"* "\n" -- both
      | ";"+
}
</script>

<script src="src/semantics.js"></script>
<script src="src/hints.js"></script>
<script>
function selectAll(id) {
  document.getElementById(id).select();
}
var bash = ohm.grammarFromScriptElement();
var s = bash.semantics();
var errMessage;
var warned = false;
s.addOperation(
  'toJS(indent)',
  source2sourceSemantics);

function loadTranslatedText() {
  globalInclude = document.getElementById('checkbox1').checked
  var script = document.getElementById('bashcode').value.trim() + '\n';
  var m = bash.match(script);
  errMessage = m.failed() ? m.message : null;
  warned = false;
  if (!errMessage) {
    var n = s(m);
    var replacementText = n.toJS(0);
    document.getElementById('jscode').innerHTML = replacementText;
  }
}

/*
 * The drag-and-drop code is adapted from http://stackoverflow.com/a/12215040
 */
function handleFileSelect(evt) {
  evt.stopPropagation();
  evt.preventDefault();

  var files = evt.dataTransfer.files; // FileList object.
  var reader = new FileReader();
  reader.onload = function(event) {
    document.getElementById('bashcode').value = event.target.result;
    loadTranslatedText();
  }
  reader.readAsText(files[0],"UTF-8");
}

function handleDragOver(evt) {
  evt.stopPropagation();
  evt.preventDefault();
  evt.dataTransfer.dropEffect = 'copy'; // Explicitly show this is a copy.
}

$('document').ready(function() {
  // Setup the dnd listeners.
  var dropZone = document.getElementById('bashcode');
  dropZone.addEventListener('dragover', handleDragOver, false);
  dropZone.addEventListener('drop', handleFileSelect, false);
  $('#bashcode').keyup(loadTranslatedText);
  $('#checkbox1').change(loadTranslatedText);
  var timeout;
  $('#bashcode').keypress(function() {
    if(timeout) {
      clearTimeout(timeout);
      timeout = null;
    }
    timeout = setTimeout(function() {
      if (errMessage) {
        console.error(errMessage);
        warned = true;
      }
    }, 3000);
  });
  loadTranslatedText();

  // Load in a hint
  var idx = Math.floor(Math.random() * hints.length);
  document.getElementById('hint-body').innerHTML = hints[idx];
});
</script>
</head>
<body>
<h1>Bash to ShellJS Translator</h1>
<blockquote
cite="https://github.com/shelljs/shelljs#shelljs---unix-shell-commands-for-nodejs">
"Say goodbye to those gnarly Bash scripts!"
-- @arturadib, <a
href="https://github.com/shelljs/shelljs#shelljs---unix-shell-commands-for-nodejs">ShellJS
README</a>
</blockquote>
<p>Like it? Check out the project <a href="https://github.com/nfischer/BashToShellJS">on Github</a></p>
<input type="checkbox" name="globalInclude" id="checkbox1" checked>Include in global namespace <i><a href="http://github.com/shelljs/shelljs#global-vs-local">What is this?</a></i>
<br>
<div id="hint-box">
<p class="hint-text">Hint:</p>
<p class="hint-text" id="hint-body"></p>
</div>
<table>
<tr>
<td>
<div class="code-box">
<h2>Shell Input Code</h2>
<textarea id="bashcode" autocomplete="off" autocorrect="off"
autocapitalize="off" spellcheck="false" cols="80" rows="16" wrap="off">
#!/bin/bash
echo "Type something, paste in some code, or drag-and-drop a shell script"</textarea>
<div class="container">
<input type="button" class="selector" value="Select this script" onclick="selectAll('bashcode')" id="button1">
</div>
</div>
</td>
</tr>
<tr>
<td>
<div class="code-box">
<h2>Generated ShellJS Output</h2>
<textarea id="jscode" autocomplete="off" autocorrect="off"
autocapitalize="off" spellcheck="false" cols="80" rows="16" wrap="off" readonly>
</textarea>
<div class="container">
<input type="button" class="selector" value="Select this script" onclick="selectAll('jscode')" id="button2">
</div>
</div>
</td>
</tr>
</table>
</body>
</html>
