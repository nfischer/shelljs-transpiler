Bash {
  Script
      = (Shebang)? allwhitespace* CmdSequence? semicolon*
  CmdSequence = NonemptyListOf<Cmd, semicolon>
  Shebang = "#!" (~"sh" any)* "sh\n"

  Cmd = FunctionDecl
      | CodeBlock
      | IfCommand
      | comment
      | ForCommand
      | WhileCommand
      | CmdWithComment
      | PipeCmd
      | Export
      | Assignment
      | SimpleCmd
      | ("" ~keyword)

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = "if" #(allwhitespace+) Conditional semicolon "then" #(allwhitespace+) CmdSequence
  ElseIfThen
      = semicolon "elif" #(allwhitespace+) Conditional semicolon "then" #(allwhitespace+) CmdSequence
  ElseCase
      = semicolon "else" #(allwhitespace+) CmdSequence
  EndIf = semicolon "fi"

  Conditional
      = TestCmd   -- test
      | SimpleCmd -- cmd

  ForCommand
      = "for" "((" ControlStruct "))" semicolon "do" #(allwhitespace+) CmdSequence Done
          -- c_style
      | "for" id "in" (Call | arrayReference) semicolon "do" #(allwhitespace+) CmdSequence Done          -- for_each

  ControlStruct
      = Assignment ";" id BinaryOp Bashword ";" (~")" Bashword)

  WhileCommand
      = "while" #(allwhitespace+) Conditional semicolon "do" #(allwhitespace+) CmdSequence Done

  Done = semicolon "done"

  FunctionDecl
      = ("function" #(space+))? id_std "()"  allwhitespace* CodeBlock // "foo()"
      | "function"  #(space+)   id_std "()"? allwhitespace* CodeBlock // "function foo"

  CodeBlock
      = "{" allwhitespace* CmdSequence allwhitespace* "}"

  BinaryOp (Binary infix operator)
      = "=="  | "=" | "-eq"
      | "!="  | "-ne"
      | "-lt" | "\\<"
      | "-gt" | "\\>"
      | "-le"
      | "-ge"

  UnaryOp (Unary operator)
      = "-z" | "-n" | "-b" | "-c" | "-d" | "-e" | "-f" | "-L" | "-p" | "-S"

  SequenceCmd
      = Cmd semicolon Cmd -- std
      | Cmd semicolon -- null

  PipeCmd = (PipeCmd | SimpleCmd) "|" allwhitespace* (PipeCmd | SimpleCmd)

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = SimpleCmdBase Redirect* "&"?

  Redirect
      = ">" Bashword
      | ">>" Bashword

  SimpleCmdBase
      = TestCmd
      | CmdName Bashword* -- std

  TestCmd
      = "test " "!"? UnaryOp Bashword               -- unary
      | "test " "!"? Bashword BinaryOp Bashword     -- binary
      | TestCmd_unaryBracket
      | TestCmd_binaryBracket
      | TestCmd_str
  TestCmd_unaryBracket
      = #("[[" space+) "!"? UnaryOp Bashword "]]"
      | #("[" space+) "!"? UnaryOp Bashword "]"
  TestCmd_binaryBracket
      = #("[[" space+) "!"? Bashword BinaryOp Bashword "]]"
      | #("[" space+) "!"? Bashword BinaryOp Bashword "]"
  TestCmd_str
      = #("[[" space+) "!"? Bashword "]]"
      | #("[" space+) "!"? Bashword "]"

  CmdName = (~keyword Bashword #(~alnum))
  comment = space* "#" (~"\n" any)*
  /* modOptions = ("-" |"+" ) letter+ */

  Bashword
      = Call
      | ArrayLiteral
      | arrayLength
      | arrayReference
      | number
      | bareWord

  ArrayLiteral
      = "(" allwhitespace* (Bashword allwhitespace*)* ")"

  Call
      = "$(" Cmd ")"
      | "\"$(" Cmd ")\""
      | "`" Cmd "`"
      | "\"`" Cmd "`\""

  arrayReference
      = "${" id "[@]}"
      | "\"${" id "[@]}\""

  arrayLength
      = "${#" id "[@]}"

  reference
      = "$" id -- simple
      | "${" id "}" -- wrapped
      | "${" id ":" digit+ ":"? digit* "}" -- substr
      | "${" id ("//" | "/") alnum* "/"? alnum* "}" -- substit
      | "${#" id "}" -- length

  number
      = "-"? digit+

  bareWord
      = barewordChar+

  barewordChar
      = stringLiteral -- str
      | (~(badchars | "\\") (reference | any)) -- normal
      | "\\" any -- escape

  keyword
      = keywordRoot ~alnum

  keywordRoot
      = "if" | "then" | "else" | "elif" | "fi" | "for" | "done" | "do" | "function"

  stringLiteral
      = singleString
      | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
      = (~"'" any)
      | "\\" any -- escape

  notDoubleQuote
      = (~("\"" | "\\") (reference | any))
      | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\"" | "`" | "(" | ")" | "&" | "{" | "}"
  Export
      = "export " Assignment -- assign
      | "export " id -- bare
  Assignment
      = ("local " | "readonly ")? #(id_std "=" ~space) Bashword?

  id  = (~keyword (letter | "_") (alnum | "_")*) -- std
      | digit+ -- arg
      | "?" -- error
      | "#" -- argLength
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon
      = ";"* "\n" -- both
      | ";"+
}
